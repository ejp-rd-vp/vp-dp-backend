PREFIX ejprd: <https://w3id.org/ejp-rd/vocabulary#>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX fdp-o: <https://w3id.org/fdp/fdp-o#>

SELECT * WHERE {
    # select all resources (any resource, not limited to a specific type) that define a vpConnection property
    ?resource ejprd:vpConnection ?connection ;

            # get the resource title (always assumed to be there)
            dcterms:title ?resourceName ;

            # get the created timestamp (always assumed to be there)
            fdp-o:metadataIssued ?created ;

            # get the modification timestamp (always assumed to be there)
            fdp-o:metadataModified ?updated .

    # get the landing page, if any (not all resources seem to define this?)
    OPTIONAL { ?resource dcat:landingPage ?resourceHomePage }

    # get the description, if any (not all resources seem to define this?) 
    OPTIONAL { ?resource dcterms:description ?resourceDescription }

    # get the publisher's logo, if any
    OPTIONAL { ?resource dcterms:publisher [ foaf:logo ?logo ] }
    
    OPTIONAL {
        # get the distribution of this resource (so this limits it to Catalogs, Datasets, PatientRegistries, and BioBanks)
        ?resource dcat:distribution ?dist .

        # for the distribution, get the DataService
        ?dist dcat:accessService ?service .
        
        # for the DataService, get the endpoint url and its type
        ?service dcat:endpointURL ?endpointUrl ;
                 dcterms:type ?type .
    }
}
